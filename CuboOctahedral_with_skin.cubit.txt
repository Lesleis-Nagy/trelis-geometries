set warning=off
###############################################################################
#
# FT       - face truncation factor (0.7 is what I've been using).
# Ipercent - is the elongation percentage.
# ESVD     - equivalent spherical volume diameter.
# skin     - skin thickness of outer layer in microns
# gap      - gap between core and outer layer 
# S        - ESVD scaling factor (assigned below).
# pi       - the numeric value of the mathematical constant pi
# esize    - the element size
#
# {FT=0.7}
# {Ipercent=0.0}
# {ESVD=0.050}
# {skin = .01}
# {gap = .01}
# {S=0.0}
# {pi=atan2(0,-1)}
# {esize=0.008}
# 
###############################################################################

Reset
warning=off

create vertex x  1 y  0 z  0   # 1
create vertex x -1 y  0 z  0   # 2
create vertex x  0 y  1 z  0   # 3
create vertex x  0 y -1 z  0   # 4
create vertex x  0 y  0 z  1   # 5
create vertex x  0 y  0 z -1   # 6

create surface vertex 1 3 5
create surface vertex 1 3 6
create surface vertex 1 4 5
create surface vertex 1 4 6

create surface vertex 2 3 5
create surface vertex 2 3 6
create surface vertex 2 4 5
create surface vertex 2 4 6

Create volume surface all heal

compress

create surface rectangle width 3 xplane
move surface  9 location  {FT}  0.0  0.0

create surface rectangle width 3 xplane
move surface 10 location {-FT}  0.0  0.0
reverse surface 10

create surface rectangle width 3 yplane
move surface 11 location  0.0  {FT}  0.0

create surface rectangle width 3 yplane
move surface 12 location  0.0 {-FT}  0.0
reverse surface 12

create surface rectangle width 3 zplane
move surface 13 location  0.0  0.0  {FT}

create surface rectangle width 3 zplane
move surface 14 location  0.0  0.0 {-FT}
reverse surface 14

webcut body 1  with sheet surface  9
webcut body 8  with sheet surface 10
webcut body 9  with sheet surface 11
webcut body 10 with sheet surface 12
webcut body 11 with sheet surface 13
webcut body 12 with sheet surface 14

delete body 4 3 2 5 6 7
delete volume 9 12 10 8 1 11
compress

###############################################################################
# Slice the cubo in twain and translate the two halves.                       #
###############################################################################

create surface rectangle width 3 xplane name "cutplane"
webcut body 1 with sheet surface 15
delete surface 15
compress

move volume 1 x  {FT*(Ipercent/100)} include_merged
move volume 2 x {-FT*(Ipercent/100)} include_merged

create volume loft surface 11 16
unite body all
compress

###############################################################################
# Scale geometry so that it has volume equivalent to a sphere of diameter ESVD
###############################################################################

# {V=Volume(1)}
# {DUMP()}
# {S=((pi/(6*V))^(1/3))*ESVD}
volume 1 scale {S}
# {rmax=BBox_XMax("volume", 1)}
# {geomscale_with_Skin_and_Gap=((rmax+skin+gap)/rmax)}
# {geomscale_with_Gap=((rmax+gap)/rmax)}
Volume 1  copy scale {geomscale_with_Gap} nomesh
# {rmax_with_gap=BBox_XMax("volume", 2)}
Volume 1  copy scale {geomscale_with_Skin_and_Gap} nomesh
# {rmax_with_skin_and_gap=BBox_XMax("volume", 3)}
subtract body 2 from body 3
compress

###############################################################################
# Meshing                                                                     #
###############################################################################
volume 1 scheme tetmesh
volume 1 size {esize}
mesh volume all

set duplicate block elements off
block 101 add volume 2
block 101 name "OuterShell"

block 202 add volume 1
block 202 name "core"

block all element type tetra4

export patran "/Users/williams/Desktop/test.pat" overwrite
